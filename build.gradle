apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.xblackcat.sjpu'
def projectTitle = 'SJPU Saver'

version = new File('build.txt').getText('UTF-8')
ext.commonManifest = manifest {
    attributes(
            "Implementation-Title": projectTitle,
            "Implementation-Version": version
    )
}


defaultTasks 'clean', 'buildAll'

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.xblackcat.sjpu', name: 'sjpu-utils', version: '[0.1.4,1.0)'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '[1.5,2.0)', transitive: false
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '[3.4,4.0)'
    compile group: 'commons-logging', name: 'commons-logging', version: '[1.1,2.0)'
    compile group: 'commons-net', name: 'commons-net', version: '[3.3,4.0)'
    compile group: 'com.jcraft', name: 'jsch', version: '[0.1,1.0)'

    testCompile group: 'junit', name: 'junit', version: '[4.12,)'
    testCompile group: 'com.h2database', name: 'h2', version: '[1.4,2.0)'

    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: '[2.4,3.0)'
    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-core', version: '[2.4,3.0)'
}

jar {
    manifest = project.manifest {
        attributes(
                "Main-Class": 'org.xblackcat.sjpu.saver.SaverUtils',
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
        from commonManifest
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
    manifest = project.manifest {
        from commonManifest
    }
}

javadoc {
    title = projectTitle
}

task javadocJar(dependsOn: javadoc, type: Jar) {
    from javadoc.outputs.files
    classifier = 'javadoc'
    manifest = project.manifest {
        from commonManifest
    }
}

//[compileJava, compileTestJava]*.options.collect {options ->
//  options.deprecation = true
//  options.compilerArgs.add '-Xlint'
//  options.compilerArgs.add '-Xlint:-unchecked'
//  options.compilerArgs.add '-Xlint:-serial'
//  options.compilerArgs.add '-Xlint:-rawtypes'
//}


task sourcesZip(type: Zip) {
    from project.projectDir
    include 'src/**'
    include 'test/**'
    include 'build.*'
    include 'LICENSE'
    into('lib') {
        from configurations.testCompile
        include '*'
    }
    classifier = 'sources'
}

task binZip(dependsOn: 'jar', type: Zip) {
    from jar.outputs.files
    into('dep') {
        from configurations.compile
    }
    classifier = 'bin'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'test/java'
        }
        resources {
            srcDir 'test/res'
        }
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: mavenCentralUrl) {
                authentication(userName: mavenCentralUser, password: mavenCentralPassword)
            }

            pom.project {
                name projectTitle
                packaging 'jar'
                description 'Library for uploading data as stream to destination specified by URL. Supported protocols: file:// sftp:// ftp:// ftps://. Optionaly result file compression coule be specified: GZip, BZip2, XZ'
                url 'https://github.com/xBlackCat/sjpu-saver'

                scm {
                    url 'scm:git@github.com:xBlackCat/sjpu-saver.git'
                    connection 'scm:git@github.com:xBlackCat/sjpu-saver.git'
                    developerConnection 'scm:git@github.com:xBlackCat/sjpu-saver.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'xBlackCat'
                        name 'Zhukov Alexei'
                        email 'xblackcat@gmail.com'
                    }
                }
            }

        }
    }
}


task buildAll(dependsOn: ['build', 'sourcesJar', 'binZip', 'sourcesZip', 'javadocJar']) {}